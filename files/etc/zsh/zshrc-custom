#!/bin/zsh
# This is meant to be sourced; shebang line is for editors

umask 027

# Let me type a ! in a filename without escaping it
set +o histexpand

# http://www.zsh.org/mla/workers/1996/msg00615.html
HISTSIZE=11000
SAVEHIST=10000
HISTFILE=~/.config/zsh/history

# Use modern completion system
autoload -Uz compinit
compinit

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# Don't require typing the 'cd ' to change directory
setopt autocd


### Essential settings copied from the stock .zshrc

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=2
#eval "$(dircolors -b)"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'


### Terminal title configuration

# precmd is run before the prompt is displayed
precmd() {
	# Set the terminal title to 'hostname# pwd'
	print -Pn '\e]0;%m# %~\a'
}

# preexec is run after you hit Enter, but before the command is run
preexec() {
	# Set the terminal title to 'hostname# the command being run'
	#
	# See http://www.zsh.org/mla/users/2004/msg00948.html
	# "Re: question about quoting % in preexec" to understand
	# the escaping strategy here.
	print -Pn '\e]0;%m# '
	print -rn -- "$1"
	print -Pn '\a'
}


### Helper functions for prompt

fast-cat() {
	echo "$(< $1)"
}

print-git-branch() {
	if [[ -f ".git/HEAD" ]]; then
		echo ":${$(fast-cat .git/HEAD)#ref: refs/heads/}"
	fi
}


### System-wide environment

setopt promptsubst # needed for \$ evaluation
export PS1="$(print '%{\e[1;31m%}')%n$(print '%{\e[0;37m%}')@$(print '%{\e[0;36m%}')%m$(print '%{\e[0m%}'):$(print '%{\e[0;32m%}')%d$(print '%{\e[0m%}')\$(print-git-branch)# "

# Need to be in a comma locale for sort -n to properly sort numbers with commas
export LANG=en_US.UTF-8

# Keep these files out of HOME
export ICEAUTHORITY="$XDG_RUNTIME_DIR"/ICEauthority
export XAUTHORITY="$XDG_RUNTIME_DIR"/Xauthority

export EDITOR=nano
export PAGER=less
export LESS="-IMx4R" # -R for color for git log
export LESSHISTFILE=-


### System-wide aliases

alias cp='cp -a'
# preserve extended attributes (-X is not included in -a)
alias rsync='rsync -X'

alias mv='mv -i'
# Make du and df report sizes in bytes, with commas, like our ls
alias du="du --block-size=\'1"
alias df="df -T --block-size=\'1"

# List all files, including hidden files, sorted by mtime, most recent last
alias l='ls -lArt'
alias ll='ls -l'

alias tree='tree -F'

alias git=pinned-git
alias gpr='git pull --rebase'
alias gb='git branch'
alias aggro='git gc --aggressive'
alias gd='git diff'
alias gbsu='git branch --set-upstream'
alias gs='git status'
alias gc='git commit'
alias gca='git commit -a'
alias gcam='git commit -a -m'

# Typos caused by keyboard matrix circuit not releasing Shift fast enough
alias Grep=grep

# Starts an process with arguments and restarts it when it exits,
# slowing down the restart speed if the process returns a non-0
# exit code.  Useful for development-mode servers that exit when
# they detect source changes.
restarter() {
	local wait=0.0625
	while true; do
		echo -n -e '\e[33m'
		print -n -P '[restarter] %D %*'
		echo -E " starting ${*}"
		echo -n -e '\e[39m'
		# eval "$@" is need to make `restarter somealias args` work
		# eval "${(q)@}" is need to make that work with spaces
		eval "${(q)@}"
		local exitcode=$?
		if [[ $exitcode -eq 0 ]]; then
			wait=0.0625
		else
			wait=$(( wait * 2))
			# zsh adds a needless trailing . on integers
			wait=${wait%%.}
			if [[ $wait -gt 4 ]]; then
				wait=4
			fi
		fi
		echo -n -e '\e[33m'
		echo -E "[restarter] Child exit code: $exitcode.  Restarting in $wait seconds..."
		echo -n -e '\e[39m'
		sleep $wait
	done
}
